(
"Parameters: ";
$system := ["P100-PCIe"][0];
$num_blocks := [50000][0];
$component := ["find_blocks", "generate", "simple_apply"][2];

$stage := {
    "find_blocks": "generate",
    "generate": "generate",
    "simple_apply": "apply"
}~>$lookup($component);
$flops := function($component, $num_blocks, $block_size) {
    $component = "generate" ? (2 * ($block_size~>$power(3)) * $num_blocks) :
    $component = "simple_apply" ? ( 2 * ($block_size~>$power(2)) * $num_blocks) :
    (($block_size~>$power(2)) * $num_blocks)
};

$filter_result := function($dataset, $blocks) {
($dataset[problem.num_blocks=$blocks].{
    "block_size": problem.block_size,
    "preconditioner": preconditioner
})^(block_size)
};

$cg_set := content[dataset.system=$system]~>$filter_result($num_blocks);
$no_cg_set := content[dataset.system=($system & "-no-cg")]~>$filter_result($num_blocks);

$extract_gen_performance := function($transformed) {
    $transformed.(
        ($component~>$flops($num_blocks, block_size)) /
        (((preconditioner~>$lookup("jacobi-" & block_size))~>$lookup($stage)).components~>$lookup($component))
    )
};

$cg_gen := $cg_set~>$extract_gen_performance();
$no_cg_gen := $no_cg_set~>$extract_gen_performance();
{
    "type": "line",
    "data": {
        "labels": [1..32],
        "datasets": [
            {
                "label": "cooperative groups API",
                "data": $cg_gen,
                "borderColor": "hsl(120,55%,60%)",
                "backgroundColor": "hsl(120,55%,60%)",
                "fill": "false"
            },
            {
                "label": "low-level API",
                "data": $no_cg_gen,
                "borderColor": "hsl(0,55%,60%)",
                "backgroundColor": "hsl(0,55%,60%)",
                "fill": "false"
            }
        ]
    },
    "options": {
        "title": {
            "display": true,
            "text": $system & " performance of '" &
                    $component & "' step of '" & $stage & "' stage"
        },
        "scales": {
            "xAxes": [{
                "scaleLabel": {
                    "display": true,
                    "labelString": "Block size"
                }
            }],
            "yAxes": [{
                "scaleLabel": {
                    "display": true,
                    "labelString": "GFlop/s"
                }
            }]
        },
        "tooltips": { "mode": "index" }
    }
}
)
