(
"Parameters: ";
$cases := [
    {"label": "full", "system": "juwels-V100-SXM2-adaptive", "variant": "0,0" },
    {"label": "adaptive", "system": "juwels-V100-SXM2-adaptive", "variant": "autodetect" }
];
$num_blocks := [50000][0];
$stage := ["generate", "apply"][0];
$sizes := [1..32];

"Utilities";
$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$filter_data := function($dataset) {
    ($cases~>$map(function($v, $i) {(
        $dataset[
                dataset.system=$v.system and
                problem.num_blocks=$num_blocks and
                problem.block_size in $sizes].(
            $bsize := problem.block_size;
            (preconditioner
                ~>$lookup("jacobi-" & $bsize & "-" & $v.variant)
                ~>$lookup($stage)
            ).components~>$each(function ($v2, $k) {({
                "group": $i,
                "label": $v.label,
                "component": $k,
                "block_size": $bsize,
                "time": $v2
            })})
        )
    )})).*
};

$transform := function($data) {(
    $systems := $data{label & ": " & component: null} ~> $keys();
    {
        "labels": $sizes,
        "datasets": $systems ~> $map(function ($v, $i) {(
            $f := $data[label & ": " & component = $v];
            $color := $getColor($systems ~> $count(), $i);
            {
                "label": $v,
                "data": ($f^(block_size)).(time / 1e6),
                "borderColor": $color,
                "backgroundColor": $color,
                "stack": $f[0].group
            }
        )}) ~> $append([])
    }
)};

$data := content~>$filter_data()~>$transform();

{
    "type": "bar",
    "data": $data,
    "options": {
        "title": {
            "display": true,
            "text": "Runtime breakdown of '" & $stage & "'"
        },
        "scales": {
            "xAxes": [{
                "stacked": true,
                "scaleLabel": {
                    "display": true,
                    "labelString": "Block size"
                }
            }],
            "yAxes": [{
                "stacked": true,
                "scaleLabel": {
                    "display": true,
                    "labelString": "Time [us]"
                }
            }]
        },
        "tooltips": { "mode": "index" }
    }
}
)
