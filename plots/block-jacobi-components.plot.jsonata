(
"Parameters: ";
$systems := {
    "daint-P100-PCIe": "baseline (DP)",
    "daint-P100-PCIe-cms": "column-major (DP)",
    "daint-P100-PCIe-i": "interleaved (DP)",
    "daint-P100-PCIe-sp": "baseline (SP)",
    "daint-P100-PCIe-cms-sp": "column-major (SP)",
    "daint-P100-PCIe-i-sp": "interleaved (SP)"
};

$num_blocks := [50000][0];
$component := ["find_blocks", "generate", "simple_apply"][2];

$stage := {
    "find_blocks": "generate",
    "generate": "generate",
    "simple_apply": "apply"
}~>$lookup($component);

$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$flops := function($component, $num_blocks, $block_size) {
    $component = "generate" ? (2 * ($block_size~>$power(3)) * $num_blocks) :
    $component = "simple_apply" ? ( 2 * ($block_size~>$power(2)) * $num_blocks) :
    (($block_size~>$power(2)) * $num_blocks)
};

$filter_result := function($dataset, $blocks) {
($dataset[problem.num_blocks=$blocks].{
    "block_size": problem.block_size,
    "preconditioner": preconditioner
})^(block_size)
};

$extract_performance := function($transformed) {
    $transformed.(
        ($component~>$flops($num_blocks, block_size)) /
        (((preconditioner~>$lookup("jacobi-" & block_size))~>$lookup($stage))
            .components~>$lookup($component))
    )
};

$dsize := $systems~>$keys()~>$count();
$datasets := $systems~>$keys()~>$map(function ($v, $i) {
    {
        "label": $systems~>$lookup($v),
        "data":  $$.content[dataset.system=$v]~>$filter_result($num_blocks)
                    ~>$extract_performance(),
        "borderColor": $getColor($dsize, $i),
        "backgroundColor": $getColor($dsize, $i),
        "fill": false
    }
});

{
    "metadata_": {
        "systems": content.dataset.system.{ $: null }~>$keys()
    },
    "type": "line",
    "data": {
        "labels": [1..32],
        "datasets": $datasets~>$append([])
    },
    "options": {
        "title": {
            "display": true,
            "text": $system & " performance of '" &
                    $component & "' step of '" & $stage & "' stage"
        },
        "scales": {
            "xAxes": [{
                "scaleLabel": {
                    "display": true,
                    "labelString": "Block size"
                }
            }],
            "yAxes": [{
                "scaleLabel": {
                    "display": true,
                    "labelString": "GFlop/s"
                }
            }]
        },
        "tooltips": { "mode": "index" }
    }
}
)
